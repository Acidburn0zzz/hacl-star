include "../arch/x64/Vale.X64.InsBasic.vaf"
include "../arch/x64/Vale.X64.InsMem.vaf"
include "../arch/x64/Vale.X64.InsVector.vaf"

module Vale.Test.X64.Vale_memcpy
#verbatim{:interface}{:implementation}

open Vale.Arch.HeapImpl
open Vale.X64.Machine_s
open Vale.X64.Memory
open Vale.X64.State
open Vale.X64.Decls
open Vale.X64.InsBasic
open Vale.X64.InsMem
open Vale.X64.InsVector
open Vale.X64.QuickCode
open Vale.X64.QuickCodes
#set-options "--z3rlimit 20"
#endverbatim

procedure memcpy(inline win:bool, ghost dst:buffer64, ghost src:buffer64)
    {:public}
    {:exportSpecs}
    {:quick}
    requires/ensures
        mem == heap0;
    requires
        locs_disjoint(list(loc_buffer(dst), loc_buffer(src)));
        buffer_readable(heap0, dst);
        buffer_readable(heap0, src);
        buffer_writeable(dst);
        valid_taint_buf64(dst, heap0, memTaint.vl_taint, Secret);
        valid_taint_buf64(src, heap0, memTaint.vl_taint, Secret);
        buffer_length(src) == 2;
        buffer_length(dst) == 2;
        win ==> rcx == buffer_addr(dst, heap0);
        win ==> rdx == buffer_addr(src, heap0);
        !win ==> rdi == buffer_addr(dst, heap0);
        !win ==> rsi == buffer_addr(src, heap0);
    ensures
        rsp == old(rsp);

        win ==>  rbx == old(rbx);
        win ==>  rbp == old(rbp);
        win ==>  rdi == old(rdi);
        win ==>  rsi == old(rsi);
        win ==>  rsp == old(rsp);
        win ==>  r12 == old(r12);
        win ==>  r13 == old(r13);
        win ==>  r14 == old(r14);
        win ==>  r15 == old(r15);
        !win ==>  rbx == old(rbx);
        !win ==>  rbp == old(rbp);
        !win ==>  r12 == old(r12);
        !win ==>  r13 == old(r13);
        !win ==>  r14 == old(r14);
        !win ==>  r15 == old(r15);
        win ==>  xmm6 == old(xmm6);
        win ==>  xmm7 == old(xmm7);
        win ==>  xmm8 == old(xmm8);
        win ==>  xmm9 == old(xmm9);
        win ==>  xmm10 == old(xmm10);
        win ==>  xmm11 == old(xmm11);
        win ==>  xmm12 == old(xmm12);
        win ==>  xmm13 == old(xmm13);
        win ==>  xmm14 == old(xmm14);
        win ==>  xmm15 == old(xmm15);

        buffer_as_seq(heap0, dst) == buffer_as_seq(heap0, src);
    //    forall(i) 0 <= i && i < 2 ==> buffer64_read(dst, i, heap0) == buffer64_read(src, i, heap0);
        modifies_mem(loc_buffer(dst), old(heap0), heap0);
    reads memTaint;
    modifies
        rax; rbx; rcx; rdx; rsi; rdi; rbp; rsp; r8; r9; r10; r11; r12; r13; r14; r15;
        xmm0; xmm1; xmm2; xmm3; xmm4; xmm5; xmm6; xmm7; xmm8; xmm9; xmm10; xmm11; xmm12; xmm13; xmm14; xmm15;
        efl; heap0;
{
    inline if (win)
    {
        Load64_buffer(rax, rdx, 0, Secret, src, 0);
        Load64_buffer(r9, rdx, 8, Secret, src, 1);
        Store64_buffer(rcx, rax, 0, Secret, dst, 0);
        Store64_buffer(rcx, r9, 8, Secret, dst, 1);
    }
    else
    {
        Load64_buffer(rax, rsi, 0, Secret, src, 0);
        Load64_buffer(rcx, rsi, 8, Secret, src, 1);
        Store64_buffer(rdi, rax, 0, Secret, dst, 0);
        Store64_buffer(rdi, rcx, 8, Secret, dst, 1);
    }
    assert(Seq.equal(buffer_as_seq(heap0, dst), buffer_as_seq(heap0, src)));
    DestroyHeaplets();
}
